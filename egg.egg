# Egg grammar for Egg grammars.
# Derived from the Leg grammar at http://piumarta.com/software/peg/peg.1.html .
#
# Author: Aaron Moss

grammar =		_ rule+ end_of_file

rule =			identifier EQUAL choice SEMI?

identifier =	< [A-Za-z_][A-Za-z_0-9]* > _

choice =		sequence ( PIPE sequence )*

sequence =		( expression | action )+

expression =	( AND | NOT )? primary ( OPT | STAR | PLUS )?

primary =		identifier !EQUAL
				| OPEN choice CLOSE
				| char_literal
				| str_literal
				| char_class
				| ANY
				| BEGIN sequence END

action =		'{' ( action | !'}' . )* '}' _

char_literal =	'\'' < character > '\'' _

str_literal =	'\"' < character* > '\"' _

char_class =	'[' < ( !']' char_range )* > ']' _

char_range =	character '-' character 
				| character

character =		'\\' [nrt\'\"\\]
				| ![\'\"\\] .

EQUAL =			'=' _
SEMI =			';' _
PIPE =			'|' _
AND =			'&' _
NOT =			'!' _
OPT =			'?' _
STAR =			'*' _
PLUS =			'+' _
OPEN =			'(' _
CLOSE =			')' _
ANY =			'.' _
BEGIN =			'<' _
END =			'>' _

_ =		 		( space | comment )*
space =			' ' | '\t' | end_of_line
comment =		'#' ( !end_of_line . )* end_of_line
end_of_line = 	"\r\n" | '\n' | '\r'
end_of_file = 	!.
