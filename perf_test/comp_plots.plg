# Plots output data from egg performance tests
# requires parameters nxml, njson, njava, pxml, pjson, pjava, dxml, djson, djava; the files to plot

set datafile separator ","

set terminal pdfcairo linewidth 3
set output 'plots/runtime-xml.pdf'

set xlabel 'Input Size (KB)'
set ylabel 'Runtime (s)'
set key top left

set style line 1 lt rgb '#fcaf3e' lw 1.5 pt 1
set style line 2 lt rgb '#f57900' lw 1.5 pt 2
set style line 3 lt rgb '#ce5c00' lw 1.5 pt 3

set style line 4 lt rgb '#8ae234' lw 1.5 pt 5
set style line 5 lt rgb '#73d216' lw 1.5 pt 7
set style line 6 lt rgb '#4e9a06' lw 1.5 pt 9

set style line 7 lt rgb '#729fcf' lw 1.5 pt 4
set style line 8 lt rgb '#3465a4' lw 1.5 pt 6
set style line 9 lt rgb '#204a87' lw 1.5 pt 8

set style line 10 lt rgb '#ad7fa8' lw 1.5 pt 10
set style line 11 lt rgb '#75507b' lw 1.5 pt 11
set style line 12 lt rgb '#5c3566' lw 1.5 pt 12

plot nxml using ($7/1000):3 with linespoints title 'Recursive XML' ls 2, \
     pxml using ($7/1000):3 with linespoints title 'Packrat XML' ls 5, \
     dxml using ($7/1000):3 with linespoints title 'Derivative XML' ls 8, \
     lxml using ($7/1000):3 with linespoints title 'DLF XML' ls 11

set output 'plots/runtime-json.pdf'

plot njson using ($7/1000):3 with linespoints title 'Recursive JSON' ls 2, \
     pjson using ($7/1000):3 with linespoints title 'Packrat JSON' ls 5, \
     djson using ($7/1000):3 with linespoints title 'Derivative JSON' ls 8, \
     ljson using ($7/1000):3 with linespoints title 'DLF JSON' ls 11

set output 'plots/runtime-java.pdf'

plot njava using ($7/1000):3 with linespoints title 'Recursive Java' ls 2, \
     pjava using ($7/1000):3 with linespoints title 'Packrat Java' ls 5, \
     djava using ($7/1000):3 with linespoints title 'Derivative Java' ls 8, \
     ljava using ($7/1000):3 with linespoints title 'DLF Java' ls 11

set output
