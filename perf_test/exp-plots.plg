# Plots output data from egg performance tests
# requires parameters nexp, pexp, dexp; the files to plot

set datafile separator ","

set terminal pdfcairo linewidth 3
set output 'plots/exp-runtime.pdf'

set xlabel 'n'
set ylabel 'Runtime (s)'
set key top left

set style line 1 lt rgb '#fcaf3e' lw 1.5 pt 1
set style line 2 lt rgb '#f57900' lw 1.5 pt 2
set style line 3 lt rgb '#ce5c00' lw 1.5 pt 3

set style line 4 lt rgb '#8ae234' lw 1.5 pt 5
set style line 5 lt rgb '#73d216' lw 1.5 pt 7
set style line 6 lt rgb '#4e9a06' lw 1.5 pt 9

set style line 7 lt rgb '#729fcf' lw 1.5 pt 4
set style line 8 lt rgb '#3465a4' lw 1.5 pt 6
set style line 9 lt rgb '#204a87' lw 1.5 pt 8

set style line 10 lt rgb '#ad7fa8' lw 1.5 pt 10
set style line 11 lt rgb '#75507b' lw 1.5 pt 11
set style line 12 lt rgb '#5c3566' lw 1.5 pt 12

plot nexp using 1:3 with linespoints title 'Recursive' ls 2, \
     pexp using 1:3 with linespoints title 'Packrat' ls 5, \
     dexp using 1:3 with linespoints title 'Derivative' ls 8 

set output 'plots/exp-runtime-l.pdf'
set logscale x

plot nexp using 1:3 with linespoints title 'Recursive' ls 2, \
     pexp using 1:3 with linespoints title 'Packrat' ls 5, \
     dexp using 1:3 with linespoints title 'Derivative' ls 8 

set output 'plots/exp-mem-use.pdf'
set nologscale x
set ylabel 'Maximum RAM Use (MB)'

plot nexp using 1:($6/1000) with linespoints title 'Recursive' ls 2, \
     pexp using 1:($6/1000) with linespoints title 'Packrat' ls 5, \
     dexp using 1:($6/1000) with linespoints title 'Derivative' ls 8

set output
