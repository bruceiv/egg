# Plots output data from egg performance tests
# requires parameters nxml, njson, njava, pxml, pjson, pjava, dxml, djson, djava, inxml, injson, injava, ipxml, ipjson, ipjava, idxml, idjson, idjava; the files to plot

set terminal epslatex size 16cm,5cm color solid

set style line 1 lt rgb 'red' lw 1.25 pt 1 ps 1.3
set style line 2 lt rgb 'red' lw 1.25 pt 2 ps 1.3
set style line 3 lt rgb 'red' lw 1.25 pt 3 ps 1.3

set style line 4 lt rgb 'green' lw 1.25 pt 5 ps 1.3
set style line 5 lt rgb 'green' lw 1.25 pt 7 ps 1.3
set style line 6 lt rgb 'green' lw 1.25 pt 9 ps 1.3

set style line 7 lt rgb 'blue' lw 1.25 pt 4 ps 1.3
set style line 8 lt rgb 'blue' lw 1.25 pt 6 ps 1.3
set style line 9 lt rgb 'blue' lw 1.25 pt 8 ps 1.3

set format x '\lbl %g'
set format y '\lbl %g'

set bmargin at screen 0.1

set key outside
set key top right
set key width -8

set datafile separator ","

set output 'runtime.tex'
set xlabel '\lbl Input Size (KB)'
set ylabel '\lbl Runtime (s)' offset 2,0

plot nxml using ($7/1000):3 with points title '\lbl Recursive XML' ls 1, \
     njson using ($7/1000):3 with points title '\lbl Recursive JSON' ls 2, \
     njava using ($7/1000):3 with points title '\lbl Recursive Java' ls 3, \
     pxml using ($7/1000):3 with points title '\lbl Packrat XML' ls 4, \
     pjson using ($7/1000):3 with points title '\lbl Packrat JSON' ls 5, \
     pjava using ($7/1000):3 with points title '\lbl Packrat Java' ls 6, \
     dxml using ($7/1000):3 with points title '\lbl Derivative XML' ls 7, \
     djson using ($7/1000):3 with points title '\lbl Derivative JSON' ls 8, \
     djava using ($7/1000):3 with points title '\lbl Derivative Java' ls 9

set output 'mem-use.tex'
set logscale xy
set ylabel '\lbl Maximum RAM Use (MB)' offset 2,0

plot nxml using ($7/1000):($6/1000) with points title '\lbl Recursive XML' ls 1, \
     njson using ($7/1000):($6/1000) with points title '\lbl Recursive JSON' ls 2, \
     njava using ($7/1000):($6/1000) with points title '\lbl Recursive Java' ls 3, \
     pxml using ($7/1000):($6/1000) with points title '\lbl Packrat XML' ls 4, \
     pjson using ($7/1000):($6/1000) with points title '\lbl Packrat JSON' ls 5, \
     pjava using ($7/1000):($6/1000) with points title '\lbl Packrat Java' ls 6, \
     dxml using ($7/1000):($6/1000) with points title '\lbl Derivative XML' ls 7, \
     djson using ($7/1000):($6/1000) with points title '\lbl Derivative JSON' ls 8, \
     djava using ($7/1000):($6/1000) with points title '\lbl Derivative Java' ls 9

set output 'backtracks.tex'
set nologscale x
set xlabel '\lbl Input Size (KB)'
set ylabel '\lbl Maximum Backtracks' offset 2,0

plot inxml using ($5/1000):3 with points title '\lbl Recursive XML' ls 1, \
     injson using ($5/1000):3 with points title '\lbl Recursive JSON' ls 2, \
     injava using ($5/1000):3 with points title '\lbl Recursive Java' ls 3, \
     ipxml using ($5/1000):3 with points title '\lbl Packrat XML' ls 4, \
     ipjson using ($5/1000):3 with points title '\lbl Packrat JSON' ls 5, \
     ipjava using ($5/1000):3 with points title '\lbl Packrat Java' ls 6, \
     idxml using ($5/1000):3 with points title '\lbl Derivative XML' ls 7, \
     idjson using ($5/1000):3 with points title '\lbl Derivative JSON' ls 8, \
     idjava using ($5/1000):3 with points title '\lbl Derivative Java' ls 9

set output 'nesting-depth.tex'
set xlabel '\lbl Input Size (KB)'
set ylabel '\lbl Maximum Nesting Depth' offset 2,0

plot inxml using ($5/1000):4 with points title '\lbl Recursive XML' ls 1, \
     injson using ($5/1000):4 with points title '\lbl Recursive JSON' ls 2, \
     injava using ($5/1000):4 with points title '\lbl Recursive Java' ls 3, \
     ipxml using ($5/1000):4 with points title '\lbl Packrat XML' ls 4, \
     ipjson using ($5/1000):4 with points title '\lbl Packrat JSON' ls 5, \
     ipjava using ($5/1000):4 with points title '\lbl Packrat Java' ls 6, \
     idxml using ($5/1000):4 with points title '\lbl Derivative XML' ls 7, \
     idjson using ($5/1000):4 with points title '\lbl Derivative JSON' ls 8, \
     idjava using ($5/1000):4 with points title '\lbl Derivative Java' ls 9
