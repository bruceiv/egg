JSON = S Object EOT
Object = LBRACE RBRACE ^| LBRACE Members RBRACE
Members = Pair (COMMA Pair)*
Pair = String COLON Value
Array = LBRACKET RBRACKET ^| LBRACKET Elements RBRACKET
Elements = Value (COMMA Value)*
Value = String ^| Number ^| Object ^| Array ^| TRUE ^| FALSE ^| NUL

String = '\"' StringR S
StringR = '\"' | '\\' ([\\\"/bfnrt] ^| "u" HEX HEX HEX HEX) StringR | . StringR

HEX = [0-9a-fA-F]
Number = (INT FRAC EXP | INT EXP | INT FRAC | INT) S
INT = ("-")? ([1-9] [0-9]* ^| "0")
FRAC = "." [0-9]+
EXP = E [0-9]+
E = "e+" ^| "e-" ^| "E+" ^| "E-" | "e" ^| "E"
TRUE = "true" S
FALSE = "false" S
NUL = "null" S
EOT = !.
COMMA = "," S
COLON = ":" S
LBRACE = "{" S
RBRACE = "}" S
LBRACKET = "[" S
RBRACKET = "]" S

S = ( [ \t\r\n] ^| "/*" S1R ^| "//" S2R )*
S1R = "*/" | . S1R
S2R = [\r\n] | . S2R
