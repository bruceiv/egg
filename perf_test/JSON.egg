JSON = S Object EOT

Object = LBRACE (Members ^| ;) RBRACE
Members = Pair MembersR
MembersR = COMMA Pair MembersR ^| ;
Pair = String COLON Value

Array = LBRACKET (Elements ^| ;) RBRACKET
Elements = Value ElementsR
ElementsR = COMMA Value ElementsR ^| ;
Value = String ^| Number ^| Object ^| Array ^| TRUE ^| FALSE ^| NUL

String = '\"' StringR S
StringR = '\"' | (!'\\' . ^| '\\' ([\\\"/bfnrt] ^| "u" HEX HEX HEX HEX)) StringR
HEX = [0-9a-fA-F]

Number = INT (FRAC ^| ;) (EXP ^| ;) S
INT = ("-" ^| ;) ([1-9] DIGITS ^| "0")
FRAC = "." DIGITP
EXP = E DIGITP
DIGITS = [0-9] DIGITS ^| ;
DIGITP = [0-9] (DIGITP ^| ;)
E = [eE] ('+' ^| '-' ^| ;)

TRUE = "true" S
FALSE = "false" S
NUL = "null" S

EOT = !.
COMMA = "," S
COLON = ":" S
LBRACE = "{" S
RBRACE = "}" S
LBRACKET = "[" S
RBRACKET = "]" S

S = ( [ \t\r\n] ^| "/*" S1R ^| "//" S2R ) S ^| ;
S1R = "*/" | . S1R
S2R = [\r\n] | . S2R
