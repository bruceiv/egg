JSON = _* Object !.
Object = LBRACE RBRACE | LBRACE Members RBRACE
Members = Pair (COMMA Pair)*
Pair = String COLON Value
Array = LBRACKET RBRACKET | LBRACKET Elements RBRACKET
Elements = Value (COMMA Value)*
Value = String | Number | Object | Array | TRUE | FALSE | NUL
String = ("\"\"" | "\"" CHAR+ "\"") _*
CHAR = (!([\"\\]) .) | "\\" [\\\"/bfnrt] | "u" HEX HEX HEX HEX
HEX = [0-9a-fA-F]
Number = (INT FRAC EXP | INT EXP | INT FRAC | INT) _*
INT = ("-")? ([1-9] [0-9]* | "0")
FRAC = "." [0-9]+
EXP = E [0-9]+
E = "e+" | "e-" | "E+" | "E-" | "e" | "E"
TRUE = "true" _*
FALSE = "false" _*
NUL = "null" _*
COMMA = "," _*
COLON = ":" _*
LBRACE = "{" _*
RBRACE = "}" _*
LBRACKET = "[" _*
RBRACKET = "]" _*
_ = [ \t\r\n]
    | "/*" (!"*/" .)* "*/"
    | "//" (![\r\n] .)* [\r\n]
